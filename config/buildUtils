import os
import sys
import os.path
import glob

Import('*')

# function to change the file path according to the visual studio project position
def toVCProjPath( l ):	
	return map( lambda x: os.path.join( '..' , File( x ).srcnode().path), l )
	
# create visual studio project for all source and header files, add to single solution	
def createVisualStudioProject(env, sources, header, aliasName = 'vcproj'):
	global msvsTopDirProjects
	if 'vcproj' in COMMAND_LINE_TARGETS:
		vcproj = env.MSVSProject( target = '#/vcproj/'+aliasName + env[ 'MSVSPROJECTSUFFIX' ], 
			srcs = toVCProjPath( sources ), incs = toVCProjPath( header ), 
			buildtarget = Dir( '.' ), auto_build_solution = 0 )
		# build target 'vcproj' for solution creation
		Alias (['vcproj', 'all'], vcproj)
		msvsTopDirProjects += [ vcproj ]
	return

# create a single component library file from the sources and setup the install targets. Components will be installed to the component directory (e.g. bin/ubitrack)
def setupSingleComponentBuild(env, sources, libName, aliasName = 'components'):
	# compile library
	lib = env.SharedLibrary( libName + configuration_suffix, sources )	
	# single library build
	libNames =  [ libName + configuration_suffix ];
	# create build targets for compile and install
	Alias( [ 'install-all', 'install-'+aliasName ], installLibs( env, install_component_prefix, '', lib, True ) )
	Alias( [ aliasName, 'all' ], lib  )
	
	return libNames;

# create a library file for every cpp file from the sources and setup the install targets. Components will be installed to the component directory (e.g. bin/ubitrack)
def setupComponentBuild(env, sources, aliasName = 'components'):
	all_libs = []
	libNames = []
	# compile library for every source file
	for comp in sources:
		# library name constructed from source file name
		libName = os.path.splitext( os.path.basename( comp ) )[ 0 ]+ configuration_suffix
		lib = env.SharedLibrary( libName , comp )
		all_libs += lib
		libNames += libName
		
	# create build targets for compile and install
	Alias( [ 'install-all', 'install-'+aliasName ], installLibs(env, install_component_prefix,'', all_libs, True ) )
	Alias( [aliasName, 'all' ], all_libs )
	

	return libNames

# create a single library file from the sources and setup the install targets. The library will be installed to the binary directory (e.g. bin)	
def setupLibraryBuild(env, sources, libName, aliasName = 'components'):
	# compile library
	lib = env.SharedLibrary( libName + configuration_suffix, sources )	
	Default( lib )
	libNames =  [ libName + configuration_suffix ];	
	# create build targets for compile and install
	Alias( [ 'install-all', 'install-'+aliasName ], installLibs( env, install_binary_prefix, install_library_prefix, lib ) )
	Alias( [ aliasName, 'all' ], lib  )
	return libNames

# create a single static library file from the sources and setup the install targets. The library will be installed to the binary directory (e.g. bin)	
def setupStaticLibraryBuild(env, sources, libName, aliasName = 'components'):
	# compile library
	lib = env.StaticLibrary( libName + configuration_suffix, sources )
	Default( lib )
	libNames =  [ libName + configuration_suffix ];
	# create build targets for compile and install
	Alias( [ 'install-all', 'install-'+aliasName ], installLibs( env, install_binary_prefix, install_library_prefix, lib ) )
	Alias( [ aliasName, 'all' ], lib  )
	return libNames

# create a single application executable from the sources and setup the install targets. The application will be installed to the binary directory (e.g. bin)	
def setupAppBuild(env, sources, appName, aliasName = 'apps'):
	# compile program
	lib = env.Program( appName + configuration_suffix, sources )
	Default( lib )
	libNames =  [ appName + configuration_suffix ];
	# create build targets for compile and install
	Alias( [ 'install-all', 'install-'+aliasName ], env.Install(install_binary_prefix, lib ) )
	Alias( [ aliasName, 'all' ], lib  )
	return libNames

# create a single application library from the sources and setup the install targets. The library will be installed to the binary directory (e.g. bin)	
def setupAppLibBuild(env, sources, appName, aliasName = 'apps'):
	# compile library
	lib = env.SharedLibrary( appName + configuration_suffix, sources )
	Default( lib )
	libNames =  [ appName + configuration_suffix ];
	# create build targets for compile and install
	Alias( [ 'install-all', 'install-'+aliasName ], env.Install(install_binary_prefix, lib ) )
	Alias( [ aliasName, 'all' ], lib  )
	return libNames

# setup the build targets for the document folder	
def setupDocInstall(env, docRoot,  aliasName = 'utql'):
	utqlDirs = globDirectories(  os.path.join( docRoot, '*' ))
	utqlFiles = glob.glob( os.path.join(getCurrentPath(), docRoot, '*.*' ) )	
	instDir = os.path.join(install_document_prefix ,docRoot)		
	for n in utqlDirs:
		setupDocInstall(env, n, aliasName)
        
	for n in utqlFiles:		
		instFile = env.Install(instDir, n )			
		Alias( [ 'documents', 'install-all', 'install-'+aliasName ],  instFile)
		
	return

# setup build targets to install header files into the 'include' directory	
def setupIncludeInstall(env, sources, suffix, aliasName = 'includes'):	
	instDir = os.path.join(install_include_prefix ,suffix)
	Alias( [ 'includes', 'all', 'install-all', 'install-'+aliasName ], env.Install(instDir, sources ) )	
	return
	
# generate console output that describes how to link against the library	
def generateHelp(options):
	keys = ['CPPDEFINES', 'CPPPATH', 'LIBPATH', 'LIBS']
	values = {}
	for key in keys:
		if(options.has_key(key)):
			values[key] = options[key]
		else:
			values[key] = ''

	
	compileAndLinkHelp = """

	To link against the utcore library from outside the Ubitrack build system, please use the following options:

   	CPPDEFINES : %s
   	CPPPATH    : %s
   	LIBPATH    : %s
   	LIBS       : %s

	""" % ( values[ 'CPPDEFINES' ], pathListToStringList( values[ 'CPPPATH' ] ),
 	pathListToStringList( values[ 'LIBPATH' ] ), values[ 'LIBS' ] )
	

	# generate help text for command line
	Help( compileAndLinkHelp )


Export('createVisualStudioProject', 'setupComponentBuild',  'setupSingleComponentBuild' ,'setupLibraryBuild', 'setupStaticLibraryBuild', 'generateHelp', 'setupAppBuild', 'setupDocInstall', 'setupIncludeInstall', 'setupAppLibBuild')
